// Shared noise for procedural detail. Everything here is evaluated in world
// (grid) space, so detail flows continuously across cell boundaries.
export const noiseGLSL = /* glsl */ `
// Hash without sine (Dave Hoskins).
float hash13(vec3 p) {
  p = fract(p * 0.1031);
  p += dot(p, p.zyx + 31.32);
  return fract((p.x + p.y) * p.z);
}
vec3 hash33(vec3 p) {
  p = fract(p * vec3(0.1031, 0.1030, 0.0973));
  p += dot(p, p.yxz + 33.33);
  return fract((p.xxy + p.yxx) * p.zyx);
}
// Value noise in [0, 1], C1-smooth.
float vnoise(vec3 p) {
  vec3 i = floor(p), f = fract(p);
  vec3 u = f * f * (3.0 - 2.0 * f);
  return mix(mix(mix(hash13(i), hash13(i + vec3(1, 0, 0)), u.x),
                 mix(hash13(i + vec3(0, 1, 0)), hash13(i + vec3(1, 1, 0)), u.x), u.y),
             mix(mix(hash13(i + vec3(0, 0, 1)), hash13(i + vec3(1, 0, 1)), u.x),
                 mix(hash13(i + vec3(0, 1, 1)), hash13(i + vec3(1, 1, 1)), u.x), u.y), u.z);
}
float fbm3(vec3 p) {
  return 0.5 * vnoise(p) + 0.3 * vnoise(p * 2.03 + 17.1) + 0.2 * vnoise(p * 4.01 + 31.7);
}
// Interleaved gradient noise (Jimenez 2014): a cheap per-pixel jitter that TAA resolves.
float ign(vec2 px, float frame) {
  px += 5.588238 * mod(frame, 64.0);
  return fract(52.9829189 * fract(0.06711056 * px.x + 0.00583715 * px.y));
}
`;
